
plugins {
  id "java"
  id "com.github.node-gradle.node" version "7.0.1"
}


group 'org.polypheny'
description = 'The user interface for Polypheny-DB'


def versionMajor = 2
def versionMinor = 0
def versionQualifier = "-SNAPSHOT"
version = versionMajor + "." + versionMinor + versionQualifier


node {
  version = '20.11.0'
  //npmVersion = '6.14.11'
  download = true
}


npmInstall.args = ['--package-lock', '--loglevel', 'warn']

npm_run_build.dependsOn(npmInstall)

jar.dependsOn(npm_run_build)

jar {
  from 'dist' into 'webapp'
  manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project"
        attributes "Version": "$archiveVersion"
    }
}

task licenseReport(type: NpxTask) {
  command = 'license-checker-rseidelsohn'
  args = ['--json', '--production', '--files', 'build/reports/licenses/files', '--out', 'build/reports/licenses/licenseReport.json']
}


//clean.dependsOn(npm_cache_clean)

clean {
    delete "/dist"
	delete "/build"
	delete "/node_modules"
}



tasks.register('packageDistribution', Zip) {
    archiveFileName = "polypheny-ui.zip"
    destinationDirectory = file('publish')
    from file('dist')
}
packageDistribution.dependsOn('build')


import org.apache.tools.ant.taskdefs.condition.Os

task publishToDotPolypheny {
    def polyphenyHome
    def uiSubfolder = "ui/"

    // Check if the environment variable POLYPHENY_HOME exists
    if (System.getenv("POLYPHENY_HOME")) {
        polyphenyHome = System.getenv("POLYPHENY_HOME")
    } else {
        // Fallback to the user's home directory with .polypheny folder
        polyphenyHome = new File(System.getProperty("user.home"), ".polypheny").absolutePath
    }

    def targetDir = new File(polyphenyHome, uiSubfolder)
    targetDir.mkdirs()

    doLast {
        println "Copying UI files to: ${targetDir.absolutePath}"
        def sourceDir = file("dist/")
        copy {
            from sourceDir
            into targetDir
        }
    }
}
publishToDotPolypheny.dependsOn('packageDistribution')

// Add install task
task install(group: 'other', dependsOn: publishToDotPolypheny)
