
plugins {
  id "java"
  id "maven-publish"
  id "com.github.node-gradle.node" version "7.0.1"
}


group 'org.polypheny'
description = 'The user interface for Polypheny-DB'


def versionMajor = 1
def versionMinor = 0
def versionQualifier = "-SNAPSHOT"
version = versionMajor + "." + versionMinor + versionQualifier


node {
  version = '20.9.0'
  //npmVersion = '6.14.11'
  download = true
}


npmInstall.args = ['--package-lock', '--loglevel', 'warn']

npm_run_build.dependsOn(npmInstall)

jar.dependsOn(npm_run_build)

jar {
  from 'dist' into 'webapp'
  manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project"
        attributes "Version": "$archiveVersion"
    }
}


publishing {
    publications {
        "${project.name}"(MavenPublication) {
            from components.java
            pom {
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
}
task generatePom(group: 'publishing', dependsOn: "generatePomFileFor${project.name.capitalize()}Publication")


task licenseReport(type: NpxTask) {
  command = 'license-checker'
  args = ['--json']
}


//clean.dependsOn(npm_cache_clean)

clean {
    delete "/dist"
	delete "/build"
	delete "/node_modules"
}

// Add install task
task install(group: 'other', dependsOn: publishToMavenLocal)
